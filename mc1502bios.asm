; Модель памяти SMALL - генерирует .EXE файл, который затем усекается
Ideal
	model small
;---------------------------------------------------------------------------------------------------
; Макросы
;---------------------------------------------------------------------------------------------------
; Выравнивание кода для создания точки входа по указанному адресу
; (необходимо для 100% совместимости с IBM BIOS)
macro	entry	addr
	pad = str_banner - $ + addr - 0E000h
	if pad lt 0
		err	'Error room for ENTRY point'
	endif
	if pad gt 0
		db	pad dup(090h)	; Заполняем NOP-инструкциями
	endif
endm

; Макрос для дальнего перехода
macro	jmpfar	segm, offs
        db	0EAh		; Код инструкции JMP FAR
        dw	offs, segm	; Смещение и сегмент
endm

;---------------------------------------------------------------------------------------------------
include	var.asm	; Переменные
;---------------------------------------------------------------------------------------------------

; Сегмент кода
segment		code byte public 'CODE'
                assume cs:code
                org 0E000h		; BIOS начинается с адреса E000h
                assume es:nothing, ss:nothing, ds:nothing

;---------------------------------------------------------------------------------------------------
; Строковые константы для отображения при загрузке
;---------------------------------------------------------------------------------------------------
Banner:
str_banner      db ' 8088/8086/V20/V30 Modular BIOS v7.3', 0

date_full:
		db LF, CR, ' Released 26/01/2026 by Airman and RUS' , 0

enter_setup:	
		db ' Press <DEL> to Enter Setup', 0
Copyright:	
		db LF, CR, ' Copyright (C) 1989-2020, NPO "Microprocessor" 1989', LF, CR, 0
empty_string:
		db LF, CR, 0
platform_string:
		db LF, CR, ' ELECTRONIKA MC1502', 0

str_8088:
	 	db LF, CR, LF, CR,' Intel (C) 8088 at 5.33 Mhz', 0
str_v20:                                                  	
		db LF, CR, LF, CR,' NEC (C) V20 at 5.33 Mhz', 0
str_8087:
		db ' with Intel (C) 8087 FPU', 0
StrTestingSystem:
		db ' Memory testing: 000K OK', 0
StrEnteringSetup:
		db CR, LF, CR, LF, CR, LF, ' Entering Setup...', CR, LF, LF, 0
SkipMemTest:
		db ' Press <ESC> to skip memory test', 0
FailedAt:
		db  LF, CR, ' Failed at ', 0
SystemNotFound:
		db  LF, CR, ' System not found.', LF, CR, 0

str_ega_vga:
		db LF, CR, LF, CR, ' EGA/VGA Video card Installed          ', LF, CR, 0
str_cga:
		db LF, CR, LF, CR, ' CGA Video card installed              ', LF, CR, 0

str_ins_disk:	db ' Insert BOOT disk in A:', CR, LF
		db ' Press any key when ready', CR, LF, LF, 0

;---------------------------------------------------------------------------------------------------
; Таблицы портов для контроллера дисковода
;---------------------------------------------------------------------------------------------------
port_int_fdc:	; Внутренний контроллер
                db  48h	 		; Порт данных
                db  4Ch		; Порт управления
                db  4Eh		; Порт статуса
                db  4Dh		; Дополнительный порт

port_ext_fdc:	; Внешний контроллер
                db  0Ch
                db  00h
                db  08h
                db  0Ah

;---------------------------------------------------------------------------------------------------
; Таблица скоростей для COM-портов
;---------------------------------------------------------------------------------------------------
baud:
		dw    470h 	; 50 бод
                dw    341h	; 75 бод
                dw    1A1h	; 110 бод
                dw    0D0h	; 134.5 бод
                dw    068h	; 300 бод
                dw    034h	; 600 бод
                dw    01Ah	; 1200 бод
                dw    00Dh	; 2400 бод

;---------------------------------------------------------------------------------------------------
; Структура BDA (BIOS Data Area)
;---------------------------------------------------------------------------------------------------
BDA:
rs232_1:	dw    3F8h	; Адрес COM1
rs232_2:	dw    0		; Адрес COM2
rs232_3:	dw    0		; Адрес COM3
rs232_4:	dw    0		; Адрес COM4
lpt_1:		dw    62h	; Адрес LPT1 (специфика МС1502)
lpt_2:		dw    0		; Адрес LPT2
lpt_3:		dw    0		; Адрес LPT3
bios_data_seg:	dw    0		; Сегмент BDA
equip_bit:	dw    622Dh	; Слово оборудования
manufact_test:	db    0		; Флаги производителя
main_ram_size:	dw    0		; Объем основной памяти в КБ
error_codes:	dw    40h	; Коды ошибок
kb_flag_1:	db    0		; Флаги клавиатуры 1
kb_flag_2:	db    0		; Флаги клавиатуры 2
kb_alt_num:	db    0		; Альтернативный номер
kb_q_head:	dw  1Eh		; Указатель на голову очереди клавиатуры
kb_q_tail:	dw  1Eh		; Указатель на хвост очереди клавиатуры
kb_queue:	dw  1Eh		; Начало буфера клавиатуры

;---------------------------------------------------------------------------------------------------
; Процедура POST (Power-On Self Test)
;---------------------------------------------------------------------------------------------------
proc		post	near
warm_boot:				; Точка входа при сбросе/включении
                cli			; Запрещаем прерывания
                cld			; Устанавливаем направление строковых операций вперед

; Отправка POST-кода 01h (начало инициализации)
write_first_post_codes:
		mov	al, 01h
		out	mfg_port, al	; Порт диагностики

; Инициализация PPI (Programmable Peripheral Interface)
@@init_PPI:
                mov	al, 88h
                out	63h, al		; Регистр команд PPI PC/XT
                mov	al, 98h
                out	6Bh, al		; Дополнительный PPI (специфика МС1502)
                mov	al, 9
                out	62h, al		; Порт C: чтение DIP-переключателей

                mov	al, 0E0h
                out	61h, al		; Порт B: управление динамиком, клавиатурой

; POST-код 02h (PPI инициализирован)
write_ppi_post_code:
		mov	al, 02h
		out	mfg_port, al

; Инициализация PIC (Programmable Interrupt Controller)
@@init_PIC:		
                mov	al, 13h		; ICW1
                out	20h, al		; Основной PIC
                mov	al, 68h		; ICW2 (базовый вектор = 68h)
                out	21h, al
                mov	al, 9		; ICW4
                out	21h, al

; POST-код 03h (PIC инициализирован)
write_pic_post_code:
		mov	al, 03h
		out	mfg_port, al

; Инициализация PIT (Programmable Interval Timer)
@@init_PIT:
                mov	al, 36h		; Канал 0: генератор прерываний
                out	43h, al
                mov	al, 0		; Делитель частоты (65536)
                out	40h, al
                out	40h, al

                mov	al, 50h		; Канал 1: регенерация памяти
                out	43h, al
                mov	al, 4
                out	41h, al

                mov	al, 0B6h	; Канал 2: динамик
                out	43h, al
                mov	al, 2
                out	42h, al
                out	42h, al

                ; Инициализация стека и очистка BDA
                mov	ax, BDAseg
                mov	ds, ax
                assume ds:nothing
                xor	ax, ax
                mov	ss, ax		; Сегмент стека = 0000h
                mov	sp, 800h	; Указатель стека = 0800h
                mov	bp, [ds:warm_boot_flag_]
                mov	di, ax
                mov	es, ax
                mov	cx, 380h	; Очистка области данных
                rep stosw
                push	cs
                pop	ds		; DS = сегмент кода BIOS
                assume ds:nothing

; POST-код 04h (PIT инициализирован)
wriet_pit_post_code:
		mov	al, 04h
		out	mfg_port, al

; Инициализация таблицы векторов прерываний (векторы 08h-1Eh)
@@init_vec_table_1:
                mov	cx, 17h		; 23 вектора
                mov	si, offset int_vec_table_1
                mov	di, 20h		; Адрес вектора 08h в таблице прерываний

vec_table_1_loop:
                lodsw			; Читаем смещение обработчика
                stosw			; Записываем смещение
                mov	ax, cs		; Сегмент BIOS
                stosw			; Записываем сегмент
                loop	vec_table_1_loop

; POST-код 05h (первая таблица векторов инициализирована)
write_fist_vec_table_code:
		mov	al, 05h
		out	mfg_port, al

; Инициализация второй таблицы векторов (1A0h-1BFh) - специфика МС1502
@@init_vect_table_2:
                mov	cx, 8		; 8 векторов
                mov	si, offset int_vec_table_2
                mov	di, 1A0h	; Адрес альтернативной таблицы прерываний

vec_table_2_loop:
                lodsw
                stosw
                mov	ax, cs
                stosw
                loop	vec_table_2_loop

; POST-код 06h (вторая таблица векторов инициализирована)
write_secont_vec_table_code:
		mov	al, 06h
		out	mfg_port, al

; Установка заглушки для NMI (Non-Maskable Interrupt)
@@init_dummy_int:
                mov	di, 8		; Вектор 02h (NMI)
                mov	ax, offset dummy_int
                stosw
                mov	ax, cs
                stosw

; POST-код 07h (NMI заглушка установлена)
write_dummy_int_code:
		mov	al, 07h
		out	mfg_port, al

; Установка обработчика для Print Screen (Int 05h)
@@init_print_screen_int:
                mov	di, 14h		; Вектор 05h
                mov	ax, offset int_05h
                stosw
                mov	ax, cs
                stosw
                mov	ax, BDAseg
                mov	es, ax
                assume es:nothing

; POST-код 08h (Print Screen установлен)
write_print_screen_int_code:
		mov	al, 08h
		out	mfg_port, al

; Копирование шаблона BDA в сегмент 0040h
@@init_BDA:
                mov	cx, 10h		; 16 слов (32 байта)
                mov	si, offset BDA
                xor	di, di		; ES:DI = 0040:0000
                rep movsw

; Определение типа контроллера дисковода (внутренний/внешний)
@@Test_type_fdc:
                in	al, 4Bh		; Тестовый порт
                not	al
                out	4Bh, al		; Инвертируем значение
                mov	ah, al
                in	al, 4Bh		; Читаем обратно
                mov	si, offset port_int_fdc
                cmp	al, ah		; Значение сохранилось?
                jz	short init_fdc_BDA	; Да - внутренний контроллер
                mov	si, offset port_ext_fdc	; Нет - внешний контроллер

; Инициализация данных контроллера в BDA
init_fdc_BDA:
                mov	di, 42h		; BDA:3F2-3F5 - порты контроллера
                movsw
                movsw
                mov	di, 80h		; BDA:0080 - параметры дисковода
                mov	ax, 1Eh		; Размер буфера
                stosw
                mov	ax, 3Eh		; Текущий адрес
                stosw
                mov	al, 18h		; Флаги
                stosb
                mov	di, 90h		; BDA:0090 - состояние дисковода
                xor	ax, ax
                stosw
                mov	bx, ax
                mov	cx, ax
                mov	di, ax
                mov	ds, ax		; DS = 0000h
                assume ds:nothing
		call	search_rom	; Поиск дополнительных ROM

; Тестирование первых 8K ОЗУ
test_first_8K_ram:
                mov	ax, [bx]	; Читаем текущее значение
                not	ax		; Инвертируем
                mov	[bx], ax	; Записываем обратно
                cmp	ax, [bx]	; Сохранилось?
                jnz	short Print_Startup_Info	; Нет - память закончилась
                not	[word ptr bx]	; Восстанавливаем оригинальное значение
                add	ch, 8		; Следующие 8К (сегмент + 0800h)
                mov	ds, cx
                assume ds:nothing
                add	di, 20h		; Счетчик тестированной памяти
                cmp	di, 2E0h	; Проверили 8К? (2E0h * 16 = 11776 байт)
                jb	short test_first_8K_ram

; Вывод информации о системе
Print_Startup_Info:
                mov	[es:13h], di	; Сохраняем объем памяти в BDA
                mov	al, 0FCh	; Разрешаем только IRQ0 и IRQ1
                out	21h, al
                sti			; Разрешаем прерывания!
		mov	bl, 1		; Длительность звукового сигнала

		call 	beep		; Звуковой сигнал
		call	video_init	; Инициализация видео
		call	print_date	; Вывод даты
		mov	si, offset Banner
		call	print_string	; Заголовок BIOS
                mov 	si, offset Copyright
		call	print_string	; Копирайт
		mov 	si, offset empty_string
		call	print_string	; Пустая строка
		mov 	si, offset platform_string
		call	print_string	; "ELECTRONIKA MC1502"
		call	print_cpu_fpu	; Определение типа CPU/FPU
	 	
		; Позиционирование курсора для сообщения о пропуске теста
		mov	dl, 00h
		mov	dh, 09h
		mov	ah, 02h
		int 	10h
		mov	si, SkipMemTest
		call 	print_string	; "Press <ESC> to skip memory test"
        	
		; Позиционирование курсора для вывода результатов теста
		mov	dl, 0h
		mov	dh, 07h
		mov	ah, 02h
		int 	10h
                mov	si, offset StrTestingSystem
                call	print_string	; "Memory testing: 000K OK"
                mov	cx, 4h
                call	print_backspace	; Возврат на 4 позиции
                
                ; Подготовка к тесту памяти
                mov	ax, es
                mov	ds, ax
                assume ds:nothing
                xor	ax, ax
                mov	bx, ax		; BX = текущий сегмент
                mov	dx, ax		; DX = счетчик памяти в КБ
                mov	es, ax		; ES = 0000h
                assume es:nothing
				
		mov [ds:mem_test_cycle_addr], mem_test_cycle_full	; Полный тест
                jmp	short Mem_test

;---------------------------------------------------------------------------------------------------
; Основной цикл тестирования памяти
;---------------------------------------------------------------------------------------------------
Mem_test_loop:
                call	Mem_test_pattern
                jnz	short Test_error

Mem_test:
                mov	ax, es
                add	ah, 8		; Следующие 8К
                mov	es, ax
                assume es:nothing
                mov	ax, dx
                add	al, 32		; +32К
                daa			; Десятичная коррекция
                adc	ah, 0
                mov	dx, ax		; Сохраняем счетчик
                
                ; Вывод текущего значения
                mov	cx, 3
                call	print_backspace
                mov	al, dh
                call	print_AL_nibble	; Сотни
                mov	al, dl
                call	print_AL	; Десятки и единицы
                
                add	bx, 20h		; Увеличиваем счетчик параграфов
		
		; Проверка нажатия клавиши
		mov	Ah, 01h
		int	16h			; Проверка буфера клавиатуры
		jz	Mem_size_compare	; Нет нажатий - продолжаем
		
		; Клавиша нажата - читаем её
		xor	ax, ax
		int	16h
		cmp	al, 1Bh		; ESC?
		jne	Check_Del_Key	; Нет, проверяем DEL
		
		; Нажат ESC - сокращаем тест памяти
		mov word ptr [ds:mem_test_cycle_addr], mem_test_cycle_short
		jmp	Mem_size_compare

Check_Del_Key:
		cmp ax, 5300h  		; Скан-код DEL = 53h
		jne Mem_size_compare
		
		; Нажат DEL - вход в Setup (заглушка - зависаем)
		mov	si, offset StrEnteringSetup
		call	print_string
		cli
		hlt			; TODO: реализовать Setup BIOS

Mem_size_compare:
		cmp	bx, [ds:main_ram_size_]	; Достигли максимальной памяти?
		jb      short  Mem_test_loop	; Нет - продолжаем

; Очистка строки о пропуске теста
ClearMemEscString:
	 	mov	dl, 0h
		mov	dh, 7h
		mov	ah, 02h
		int 	10h

; Определение и вывод типа видеоадаптера
Print_video_type:
		call	video_type

; Загрузка операционной системы
Boot:
                mov	si, empty_string
                call	print_string
		
		; Задержка перед загрузкой
		mov	cx, 0Fh
super_delay:
		push 	cx
		mov 	cx, 0FFFFh
boot_delay:
		loop	boot_delay
		pop	cx
		loop	super_delay

       		call	clear_screen
		
		; Очистка буфера клавиатуры
		mov	ax, 1Eh
		mov	[es:1Ah], ax		; Голова буфера
		mov	[es:1Ch], ax		; Хвост буфера
		
                int	19h		; Загрузка ОС с диска

; Обработка ошибки теста памяти
Test_error:
                push	ax
                mov	si, offset FailedAt
                call	print_string	; "Failed at "
                
                ; Вывод адреса ошибки
                dec	di
                dec	di
                mov	ax, es		; Сегмент
                call	print_AX
                mov	ax, 0E3Ah	; Вывод ':'
                int	10h
                mov	ax, di		; Смещение
                call	print_AX
                mov	ax, 0E20h	; Пробел
                int	10h
                
                ; Вывод кода ошибки
                pop	ax
                xor	ax, [es:di]	; XOR между записанным и прочитанным
                call	print_AX
                
                mov	[ds:main_ram_size_], bx	; Сохраняем объем рабочей памяти
                xor	ax, ax
                int	16h		; Ожидание нажатия клавиши
endp		post

;---------------------------------------------------------------------------------------------------
; Тестирование памяти шаблонами
;---------------------------------------------------------------------------------------------------
proc		Mem_test_pattern near
                mov	ax, 0FFFFh	; Шаблон 1: все биты установлены
                call	mem_test_cycle
                jnz	short sub_exit	; Ошибка
                
                mov	ax, 0AAAAh	; Шаблон 2: чередующиеся биты 1010
                call	mem_test_cycle
                jnz	short sub_exit
                
                mov	ax, 5555h	; Шаблон 3: чередующиеся биты 0101
                call	mem_test_cycle
                jnz	short sub_exit
                
                xor	ax, ax		; Шаблон 4: все биты сброшены
		mov	AH,1		; Устанавливаем ZF=1 (успех)
endp		Mem_test_pattern

;---------------------------------------------------------------------------------------------------
; Цикл тестирования памяти (запись и проверка)
;---------------------------------------------------------------------------------------------------
proc		mem_test_cycle near
                mov     cx, [ds:mem_test_cycle_addr]	; Размер блока для теста
                xor	di, di		; ES:DI = начало блока
                rep stosw		; Заполняем блок значением AX
                
                mov     cx, [ds:mem_test_cycle_addr]
                xor	di, di
                repe scasw		; Проверяем блок
                retn			; ZF=1 если все байты совпали
endp		mem_test_cycle

;---------------------------------------------------------------------------------------------------
; Удаление символов (перемещение курсора назад)
;---------------------------------------------------------------------------------------------------
proc		print_backspace near
                mov	ax, 0E08h	; Вывод Backspace
		int	10h
                loop	print_backspace

sub_exit:				; Общая точка выхода
                retn
endp		print_backspace

;---------------------------------------------------------------------------------------------------
; Определение типа процессора и сопроцессора
;---------------------------------------------------------------------------------------------------
proc		print_cpu_fpu near
        	xor     al, al
		mov	al, 40h		; На V20 умножение не влияет на ZF
		mul	al		; На 8088 - влияет
		jz	@@have_v20	; ZF установлен? Тогда V20
		
		mov	si, offset str_8088	; 8088
		call	print_string
		jmp	fpu
		
@@have_v20:
		mov	si, offset str_v20	; V20
		call	print_string

; Проверка наличия сопроцессора 8087
fpu:		mov	ax, BDAseg
		mov 	ds, ax
		fninit			; Пытаемся инициализировать FPU
		mov	si, 0200h
		mov	[byte si+1], 0	; Очищаем байт
		fnstcw	[word si]	; Сохраняем управляющее слово FPU
		mov	ah, [si+1]
		cmp	ah, 03h		; Если AH=03h, FPU присутствует
		jne	@@no_8087
		
		or	[byte ds:10h], 00000010b	; Устанавливаем бит FPU
		mov	si, offset str_8087
		call 	print_string
		ret
		
@@no_8087:
		and	[byte ds:10h], 11111101b	; Сбрасываем бит FPU
		ret
endp		print_cpu_fpu

;---------------------------------------------------------------------------------------------------
; Вывод строки (SI -> строка, завершается нулем)
;---------------------------------------------------------------------------------------------------
proc            print_string near
print_string_loop:
                lods    [byte ptr cs:si]	; Загружаем байт из CS:SI
                or      al, al		; Конец строки?
                jz      short sub_exit
                mov     ah, 0Eh	; Функция TTY вывода
                int     10h
                jmp     short print_string_loop
endp            print_string

;---------------------------------------------------------------------------------------------------
; Вывод числа AX в шестнадцатеричном формате
;---------------------------------------------------------------------------------------------------
proc		print_AX near
                xchg	ah, al	; Сначала старший байт
                call	print_AL
                xchg	ah, al	; Затем младший
endp		print_AX

proc		print_AL near
                mov	cl, 4
                rol	al, cl		; Старшая тетрада
                call	print_AL_nibble
                rol	al, cl		; Младшая тетрада
endp		print_AL

; Преобразование двоичного числа 0-15 в ASCII символ
proc		print_AL_nibble near
                push	ax
                and	al, 0Fh
                add	al, 90h		; Магическая конверсия в hex
                daa			; через BCD
                adc	al, 40h
                daa
                mov	ah, 0Eh
                int	10h
                pop	ax
                retn
endp		print_AL_nibble

;---------------------------------------------------------------------------------------------------
; Вывод даты и приглашения в Setup
;---------------------------------------------------------------------------------------------------
proc		print_date	near
	 	mov	dl, 0h
		mov	dh, 18h		; Строка 24
		mov	ah, 02h
		int 	10h
		
		mov	si, enter_setup
		call 	print_string	; "Press <DEL> to Enter Setup"
		mov	si, date_full
		call	print_string	; Дата выпуска
		
		xor 	dx, dx		; Возврат курсора в (0,0)
		mov 	ah, 02h
		int 	10h
		ret
endp		print_date

;---------------------------------------------------------------------------------------------------
; Очистка строки
;---------------------------------------------------------------------------------------------------
proc		flush_string	near
                mov 	cx, 50h
		mov	al, 20h		; Пробел
		mov	ah, 0ah		; Функция вывода символа
		int 	10
		ret
endp		flush_string

;---------------------------------------------------------------------------------------------------
; Очистка экрана
;---------------------------------------------------------------------------------------------------
proc		clear_screen	near
		mov	dx, 184Fh	; Правый нижний угол (24,79)
		xor	cx, cx		; Левый верхний угол (0,0)
		mov	ax, 600h	; Прокрутка вверх, очистка всего экрана
		mov	bh, 7		; Атрибут: светло-серый на черном
		int	10h		; Вызов видео-сервиса
		
		mov	ah, 2		; Установка позиции курсора
		xor	dx, dx		; Верхний левый угол (0,0)
		mov	bh, 0		; Страница 0
		int	10h
		
		mov	ax, 500h	; Выбор активной страницы 0
		int	10h
		ret
endp		clear_screen

;---------------------------------------------------------------------------------------------------
; Поиск дополнительных ROM в области C000-FFFFh
;---------------------------------------------------------------------------------------------------
proc		search_rom	near
                push    bx
                push    cx
                push    dx
                push	si
                push	di
                push	ds
                push	es
                
                mov	ax, BDAseg
                mov	ds, ax
                mov	[word ptr ds:gen_use_ptr_], 3	; Смещение заглушки
                mov	[word ptr ds:gen_use_seg_], 0BE00h	; Начальный сегмент

search_loop:
                mov	ax, BDAseg
                mov	ds, ax
                add	[word ptr ds:gen_use_seg_+1], 2	; Следующий сегмент (+0800h)
                cmp	[word ptr ds:gen_use_seg_], 0FE00h	; Дошли до F000h?
                jz	short no_additional_rom
                
                mov	es, [word ptr ds:gen_use_seg_]
                assume es:nothing
                cmp	[word ptr es:0], 0AA55h	; Сигнатура ROM?
                jnz	short search_loop
                
                call	[dword ptr ds:gen_use_ptr_]	; Вызываем инициализацию ROM
		mov	es, bx
		mov	bl, 5 * 18		; Пауза 5 секунд (18.2 тика/сек)
                jmp	short search_loop

no_additional_rom:
		pop	es
		pop	ds
		pop	di
		pop	si
                pop	dx
                pop	cx
                pop	bx
		ret
endp		search_rom

;---------------------------------------------------------------------------------------------------
; Звуковой сигнал динамика
;---------------------------------------------------------------------------------------------------
proc		beep	near
		push	ax
		push	cx
		mov	al, 10110110b	; Режим таймера 3 (динамик)
		out	43h, al
		mov	ax, 528h	; Частота ~1000 Гц
		out	42h, al
		mov	al, ah
		out	42h, al
		
		in	al, 61h		; Чтение состояния порта B
		push	ax
		or	al, 00000011b	; Включить динамик и гейт таймера
		out	61h, al
		
		xor	cx, cx		; Задержка
@@delay:
		loop	@@delay
		dec	bl		; Длительность из BL
		jnz	@@delay
		
		pop	ax
		out	61h, al		; Выключить динамик
		pop	cx
		pop	ax
		ret
endp	beep

;---------------------------------------------------------------------------------------------------
; Ожидание нажатия клавиши
;---------------------------------------------------------------------------------------------------
proc	get_key	near
	mov	ah, 0		; Функция чтения клавиши
	int	16h
	ret
endp	get_key

;---------------------------------------------------------------------------------------------------
; Определение типа видеоадаптера
;---------------------------------------------------------------------------------------------------
proc    	video_type	near
		mov	ah, 12h		; Проверка наличия EGA/VGA
		mov	bx, 0FF10h
		int	10h		; Получение информации EGA
		cmp	bh, 0FFh	; Если EGA/VGA присутствует, BH != FFh
		je	is_cga		; Нет EGA/VGA - значит CGA
		mov	si, offset str_ega_vga	; Строка для EGA/VGA
		jmp	short display_video
is_cga:
		mov	si, offset str_cga	; Строка для CGA
display_video:
		call	print_string	; Вывод строки с типом видеоадаптера
		ret
endp		video_type

;--------------------------------------------------------------------------------------------------
; Initialize video card
;--------------------------------------------------------------------------------------------------
proc	video_init	near

	mov	ah, [ds:10h]			; Get equipment byte
	and	ah, 00110000b			;   extract CRT
	mov	al, 3				;   null low
	cmp	ah, 00110000b			; Monochrome?
	jz	@@init				;   yes
	mov	al, 3				; CGA 40 x 25?
	cmp	ah, 00010000b			;   yes
	jz	@@init				; CGA 80 x 25?
	mov	al, 3				;   yes
@@init:
	mov	ah, 0				; Setup subfunction
	int	10h				;   to video
	ret

endp	video_init

;---------------------------------------------------------------------------------------------------
; Подключение модулей обработчиков прерываний
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int19h.asm 	; Int 19h - Загрузчик системы
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int09h.asm 	; Int 09h - Обработчик прерывания клавиатуры (IRQ1)
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int16h.asm 	; Int 16h - Сервис клавиатуры
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int14h.asm 	; Int 14h - Сервис последовательных портов
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int13h.asm 	; Int 13h - Сервис дисковода гибких дисков
;---------------------------------------------------------------------------------------------------
;include int13h_new.asm 	; Альтернативная версия драйвера дисковода
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int1Eh.asm 	; Int 1Eh - Таблица параметров дисковода
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int17h.asm 	; Int 17h - Сервис параллельных портов
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int1Dh.asm	; Int 1Dh - Таблица параметров видео
;---------------------------------------------------------------------------------------------------

; --------------------------------------------------------------------------------------------------
include int10h.asm 	; Int 10h - Сервис видео
; --------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int12h.asm 	; Int 12h - Определение объема ОЗУ
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int11h.asm  	; Int 11h - Определение состава оборудования
;---------------------------------------------------------------------------------------------------

; --------------------------------------------------------------------------------------------------
include int1Ah.asm	; Int 1Ah - Сервис часов реального времени
; --------------------------------------------------------------------------------------------------

; --------------------------------------------------------------------------------------------------
include int08h.asm	; Int 08h - Обработчик прерывания таймера (IRQ0)
; --------------------------------------------------------------------------------------------------

; --------------------------------------------------------------------------------------------------
include dummy.asm  	; Заглушки для неиспользуемых прерываний
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include int05h.asm  	; Int 05h - Печать экрана (Print Screen)
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
; Вторая таблица прерываний МС1502 (векторы 68h-6Fh)
;---------------------------------------------------------------------------------------------------

include int68h.asm  	; Int 68h > Int 08h (IRQ0)
;---------------------------------------------------------------------------------------------------

include int69h.asm  	; Int 69h > Int 09h (IRQ1) + сканирование клавиш
;---------------------------------------------------------------------------------------------------

include int6Ah.asm  	; Int 6Ah > Int 0Ah (IRQ2)
;---------------------------------------------------------------------------------------------------

include int6Bh.asm  	; Int 6Bh > Int 0Bh (IRQ3)
;---------------------------------------------------------------------------------------------------

include int6Ch.asm  	; Int 6Ch > Int 0Ch (IRQ4)
;---------------------------------------------------------------------------------------------------

include int6Dh.asm  	; Int 6Dh > Int 0Dh (IRQ5)
;---------------------------------------------------------------------------------------------------

include int6Eh.asm  	; Int 6Eh > Int 0Eh (IRQ6)
;---------------------------------------------------------------------------------------------------

include int6Fh.asm  	; Int 6Fh > Int 0Fh (IRQ7)
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include gfx.asm		; Таблица графических символов
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
include vectors.asm  	; Таблицы векторов прерываний
;---------------------------------------------------------------------------------------------------

;---------------------------------------------------------------------------------------------------
; Точка входа при включении питания
;---------------------------------------------------------------------------------------------------
		entry   0FFF0h
proc		power	far
                jmpfar	0F000h, warm_boot	; Переход на начало BIOS
endp 		power

;---------------------------------------------------------------------------------------------------
; Сигнатура и дата BIOS
;---------------------------------------------------------------------------------------------------
date	db '26/01/26', 0	; Дата сборки
		db 0FEh		; Тип компьютера: XT

ends		code
end
